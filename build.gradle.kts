import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import groovy.lang.Closure
import net.minecrell.pluginyml.bukkit.BukkitPluginDescription
import java.net.HttpURLConnection
import java.net.URL

plugins {
    kotlin("jvm") version "1.8.0"
    id("net.minecrell.plugin-yml.bukkit") version "0.5.1"
    id("com.github.ben-manes.versions") version "0.41.0"
    id("com.palantir.git-version") version "0.12.3"
    id("dev.s7a.gradle.minecraft.server") version "1.2.0"
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("org.jmailen.kotlinter") version "3.8.0"
}

val mcVersion: String by project
val pluginVersion: String by project

// 表示用の最終バージョン
val fullVersion = "$mcVersion-$pluginVersion"
version = fullVersion

repositories {
    mavenCentral()
    maven(url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    maven(url = "https://oss.sonatype.org/content/groups/public/")
}

val shadowImplementation: Configuration by configurations.creating
configurations["implementation"].extendsFrom(shadowImplementation)

val sqliteVersion = "3.46.1.0"

dependencies {
    shadowImplementation(kotlin("stdlib"))
    compileOnly("org.spigotmc:spigot-api:$mcVersion-R0.1-SNAPSHOT")
    implementation("org.yaml:snakeyaml:2.2") // yamlファイル読み込み用
    implementation("org.xerial:sqlite-jdbc:$sqliteVersion") // SQLLite
}

configure<BukkitPluginDescription> {
    main = "com.github.ringoame196_s_mcPlugin.Main"
    version = fullVersion
    apiVersion = "1." + mcVersion.split(".")[1]
    author = "ringoame196_s_mcPlugin"
    website = "https://github.com/ringoame196-s-mcPlugin"
}

tasks.withType<ShadowJar> {
    configurations = listOf(shadowImplementation)
    archiveClassifier.set("")
    relocate("kotlin", "com.github.ringoame196_s_mcPlugin.libs.kotlin")
    relocate("org.intellij.lang.annotations", "com.github.ringoame196_s_mcPlugin.libs.org.intellij.lang.annotations")
    relocate("org.jetbrains.annotations", "com.github.ringoame196_s_mcPlugin.libs.org.jetbrains.annotations")
}

tasks.named("build") {
    dependsOn("shadowJar")
    // プラグインを特定のパスへ自動コピー
    val copyFilePath = "M:/TwitterServer/plugins/" // コピー先のフォルダーパス
    val copyFile = File(copyFilePath)
    if (copyFile.exists() && copyFile.isDirectory) {
        doFirst {
            copy {
                from(buildDir.resolve("libs/${project.name}.jar"))
                into(copyFile)
            }
        }
        doLast {
            val port = 25585
            val ip = "ringoame-server"
            val apiUrl = "http://$ip:$port/plugin?name=${project.name}"

            try {
                val url = URL(apiUrl)
                val connection = url.openConnection() as HttpURLConnection

                // タイムアウト設定（重要）
                connection.connectTimeout = 2000  // 2秒でタイムアウト
                connection.readTimeout = 2000

                connection.requestMethod = "GET"
                connection.connect()

                if (connection.responseCode == HttpURLConnection.HTTP_OK) {
                    val response = connection.inputStream.bufferedReader().use { it.readText() }
                    println("API Response: $response")
                } else {
                    println("Server responded with error code: ${connection.responseCode}")
                }

                connection.disconnect()
            } catch (e: java.net.ConnectException) {
                println("Warning: サーバーに接続できません（オフラインかもしれません）")
            } catch (e: java.net.SocketTimeoutException) {
                println("Warning: 接続がタイムアウトしました")
            } catch (e: Exception) {
                println("Warning: API通信で予期しないエラーが発生しました: ${e.message}")
            }
        }
    }
}

tasks.named("printVersion") {
    doLast {
        println(fullVersion)
    }
}

task<SetupTask>("setup")
